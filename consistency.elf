%%%%% consistency.elf
%%%%% Consistency relation between clsmap, methmap, fldmap and oflist
%%%%% Chao Sun
%%%%% $Id$
%%%%% Created at March, 2010


%%% environment for local variables

oflist : type.

oflist/0 : oflist.

oflist/+ : {x: object} ({t: nulltp} (oflist -> oflist)).


oflist-lookup : oflist -> object -> nulltp -> type.

oflist-lookup/= : oflist-lookup (oflist/+ O T _) O T.

oflist-lookup/!= : oflist-lookup (oflist/+ O1 _ L) O2 T 
      <- oflist-lookup L O2 T.


%%% check whether class types used in method-type is consistent with the clsmap.

clsmap-nulltp : clsmap -> nulltp -> type.

clsmap-nulltp/norm : clsmap-nulltp CM (nulltp/norm C _) 
      <- clsmap`domain? CM C true.
		   
clsmap-nulltp/null : clsmap-nulltp CM nulltp/null.


%%% check that all types used in method type is in class map

clsmap-methtp : clsmap -> methtp -> type.

clsmap-methtp/base : clsmap-methtp CM (methtp/base T) <- clsmap-nulltp CM T.

clsmap-methtp/arg 
   : clsmap-methtp CM (methtp/arg MTF T)
      <- clsmap-nulltp CM T
      <- ({o} clsmap-methtp CM (MTF o)).


%%% Check whether a fldmap is consistent with a clsmap

clsmap-fldmap : clsmap -> fldmap -> type.

clsmap-fldmap/0 : clsmap-fldmap _ fldmap`map/0.

clsmap-fldmap/n 
   : clsmap-fldmap CM (fldmap`map/+ F nulltp/null FM)
      <- clsmap-fldmap CM FM.
	  
clsmap-fldmap/+ 
   : clsmap-fldmap CM (fldmap`map/+ F (nulltp/norm N _) FM)
      <- clsmap`domain? CM N true
      <- clsmap-fldmap CM FM.


%%% Check whether a clsmap is consistent itself

clsmap-clsmap : clsmap -> clsmap -> type.

clsmap-clsmap/0 : clsmap-clsmap _ clsmap`map/0.

clsmap-clsmap/+
   : clsmap-clsmap M (clsmap`map/+ C FM CM)
      <- clsmap`lookup M C FM
      <- clsmap-fldmap M FM
      <- clsmap`shift C CM CMP
      <- clsmap-clsmap M CMP.


%%% Check whether a methmap is consistent with a clsmap

clsmap-methmap : clsmap -> methmap -> type.

clsmap-methmap/0 : clsmap-methmap _ methmap`map/0.

clsmap-methmap/m 
   : clsmap-methmap CM (methmap`map/+ N MT MM)
      <- clsmap`fresh CM N
      <- clsmap-methtp CM MT
      <- methmap`shift N MM MMP
      <- clsmap-methmap CM MMP.

clsmap-methmap/c 
   : clsmap-methmap CM
      (methmap`map/+ N (methtp/arg ([t] MTF t) (nulltp/norm N nn/yes)) MM)
      <- clsmap`lookup CM N FM
      <- ({t} clsmap-methtp CM (MTF t))
      <- methmap`shift N MM MMP
      <- clsmap-methmap CM MMP.


% consistency between clsmap and oflist

clsmap-oflist : clsmap -> oflist -> type.

clsmap-oflist/0 : clsmap-oflist _ oflist/0.

clsmap-oflist/+
   : clsmap-oflist CM (oflist/+ _ T L) 
      <- clsmap-oflist CM L
      <- clsmap-nulltp CM T.
